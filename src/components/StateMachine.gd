class_name StateMachine
extends Node

@export var initial_state: State

var current_state: State
var states: Dictionary = {}

func _ready() -> void:
	print("DEBUG: StateMachine _ready started (Parent should be Player)")
	for child in get_children():
		if child is State:
			states[child.name.to_lower()] = child
			child.state_machine = self
	
	if initial_state:
		initial_state.enter()
		current_state = initial_state

func _unhandled_input(event: InputEvent) -> void:
	if current_state:
		current_state.handle_input(event)

func _process(delta: float) -> void:
	if current_state:
		current_state.update(delta)

func _physics_process(delta: float) -> void:
	if current_state:
		current_state.physics_update(delta)

func transition_to(target_state_name: String, msg: Dictionary = {}) -> void:
	if not states.has(target_state_name.to_lower()):
		return

	if current_state:
		current_state.exit()

	current_state = states[target_state_name.to_lower()]
	current_state.enter(msg)
